//
// Copyright (c) 2016, Brian Frank
// Licensed under the Academic Free License version 3.0
//
// History:
//   10 Jun 2016  Matthew Giannini  Creation
//   24 Mar 2020  Matthew Giannini  multi-project build
//
plugins {
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
}

group   = 'org.projecthaystack'
version = '3.1.0'

allprojects {
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java-library'

    repositories {
        jcenter()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7

        withJavadocJar()
        withSourcesJar()
    }

    dependencies {
        // Use TestNG framework, also requires calling test.useTestNG() below
        testImplementation 'org.testng:testng:7.1.0'
    }

    test {
        useTestNG() {
            outputDirectory = file("$project.buildDir/reports/testng")
            useDefaultListeners = true
        }

        testLogging.showStandardStreams = true;

        testLogging {
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}

publishing {
    publications {
        core(MavenPublication) {
            def project = project(':haystack-core')
            from project.components.java
            artifactId project.name
        }
    }
}

bintray {
    user = project.findProperty("bintrayUser")
    key  = project.findProperty("bintrayApiKey")
    publish = true
    publications = ['core']
    pkg {
        repo    = 'maven'
        name    = 'haystack-java'
        userOrg = 'skyfoundry'
        vcsUrl  = 'https://github.com/skyfoundry/haystack-java'
    }
}
/*
project(':haystack-core') {
    bintray {
        user = project.findProperty("bintrayUser")
        key  = project.findProperty("bintrayApiKey")
        publish = true
        setPublications("haystack-core")
        pkg {
            repo    = 'maven'
            name    = 'haystack-core'
            userOrg = 'skyfoundry'
            vcsUrl  = 'https://github.com/skyfoundry/haystack-java'
        }
    }
}
*/

project(':haystack-server') {
    apply plugin: 'war'

    dependencies {
        providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

        implementation project(':haystack-core')
    }

    // Stupid war plugin disable jar for some reason. Force jar creation
    assemble.dependsOn(jar)
}

